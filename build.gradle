plugins {
    id "com.chrisgahlert.gradle-dcompose-plugin" version "0.15.1"
}
apply plugin: 'java'

sourceCompatibility = 1.6

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.mongodb:mongo-java-driver:3.0.0'
    testCompile 'junit:junit:4.12'
}

dcompose {
    // define the available networks
    networks {
        frontend
        backend
    }

    // define special volumes
    volumes {
        logdata {
            driver = 'local' // optional as it is the default driver
            driverOpts = [:] // optional
        }
    }

    // A mongo service with preserved volumes (used for running the app)
    mongo {
        image = 'mongo:3.3.9'
        portBindings = ['27017']
        preserveVolumes = true
        networks = [backend]
        aliases = ['mongoalias']
    }

    // A mongo service without preserved volumes (used for running integration tests)
    mongoTest {
        image = 'mongo:3.3.9'
        portBindings = ['27017']

        // Do not include this service in the docker-compose.yml (see task createComposeFile)
        deploy = false
    }

    // The actual app image that will be built from the Dockerfile in src/main/docker
    app {
        // When building this image, the following files are required and will be copied to
        // the directory "$buildDir/dcompose-build/app" before the image is built. It will
        // automatically honor the task dependencies brought in from the 'jar' task.
        buildFiles = project.copySpec {
            from jar
            from file('src/main/docker')
            from configurations.runtime
        }

        // Define the networks this service is connected to
        networks = [backend, frontend]

        // Define which volumes the container will have
        volumes = ['/var/spool/app']

        // Define special volume binds (disabled, as this often doesn't work, depending on your Docker setup)
        //binds = [logdata.bind('/var/log/app'), project.projectDir.path + ':/var/spool/app']

        // Passing in the environment variables for using the mongo service - host could be "mongo" or "mongoalias"
        env = ['MONGO_HOST=mongoalias', 'MONGO_PORT=27017']

        // Not only start the container, wait for it to finish
        waitForCommand = true

        // The container's stdout should be captured and redirected to System.out
        attachStdout = true

        // Make sure that the mongo service is launched before & stopped after the app service
        dependsOn = [mongo]
    }
}

// Make sure that the test DB container is always re-created (and therefore the old one is removed - if any)
// This is particularly useful if the previous test run was aborted (e.g. by pressing CTRL + C) and
// the test task's finalizer did not run.
createMongoTestContainer.outputs.upToDateWhen { false }

test {
    // Start the mongo test container before running the tests
    dependsOn dcompose.mongoTest

    // Clean up the container after completing the test run
    finalizedBy dcompose.mongoTest.removeContainerTaskName

    doFirst {
        // Pass in the configuration properties to the integration test so they can connect to the mongo test container
        systemProperty 'mongo.host', dcompose.mongoTest.dockerHost
        systemProperty 'mongo.port', dcompose.mongoTest.findHostPort(27017)
    }
    doLast {
        // Clean up the dynamic system properties in order to not mess up the UP-TO-DATE checks
        systemProperties.remove 'mongo.host'
        systemProperties.remove 'mongo.port'
    }
}

jar {
    manifest {
        attributes 'Main-Class': 'com.sample.MongoDbCounter'
    }
    from {
        // Build a fat jar with all dependencies included
        configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

// create a custom run task starting the app container and printing the resulting exit code
task run {
    dependsOn dcompose.app
    doLast {
        println "Java program successfully executed in docker container. Received exit code: $dcompose.app.exitCode"
    }
}

createComposeFile {
    useTags = true // we do not want to push the image, in order to use the image digest. Instead we rely on tags...
}
